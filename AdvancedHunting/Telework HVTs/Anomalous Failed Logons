//Query for Anomalous failed logons based on timespan and velocity thresholds
//jesse.esquivel@microsoft.com
//filter to machines with deviceRegistry tag or to a machine group
let targetDevices = DeviceInfo
        | where Timestamp > ago(24h)
        | where RegistryDeviceTag == "DEVICEREGISTRYTAG"
        | project Timestamp, DeviceId, DeviceName, RegistryDeviceTag, ReportId;
let TimeBucket = totimespan(24h); // Define the suspicious timespan threshold for failed logons
let FailedLogonCountPerTimeBucket = 5; //Define the suspicious velocity item threshold
DeviceLogonEvents
| where Timestamp > ago(24h)
| where ActionType == "LogonFailed"
| extend Event = pack("Timestamp", Timestamp, "ReportId", ReportId,"ActionType",ActionType,"DeviceName",
        DeviceName,"AccountDomain",AccountDomain,"AccountName",AccountName,"LogonType",LogonType,"RemoteIP",RemoteIP)
| summarize Events = makelist(Event), LogonCount = count(ActionType) by DeviceId, DeviceName,  bin(Timestamp, TimeBucket)
| where LogonCount > FailedLogonCountPerTimeBucket
| mv-expand Events
| project Timestamp = todatetime(Events.Timestamp), DeviceId, DeviceName = Events.DeviceName, ActionType = Events.ActionType, 
        AccountDomain = Events.AccountDomain, AccountName = Events.AccountName, LogonType = Events.LogonType, 
        RemoteIP = Events.RemoteIP, LogonCount, ReportId = tolong(Events.ReportId)
| join kind = inner(targetDevices) on DeviceId
| project-away Timestamp1, DeviceId1, DeviceName1, ReportId1
| sort by Timestamp desc
