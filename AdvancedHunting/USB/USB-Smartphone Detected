//query DeviceInfo table for logged on user, then finds all occurrences of any usb smartphone as defined by VID and/or PID and
//joins both tables so output shows both the detected smartphone PNP events and loggedon user
//jesse.esquivel@microsoft.com
let loggedOnUser = DeviceInfo
    | extend data=parsejson(LoggedOnUsers)
    | mv-expand data
    | extend userName = data.UserName
    | project userName, DeviceId, Timestamp;
DeviceEvents
    | where ActionType == 'PnpDeviceConnected'
    | extend PNP = parsejson(AdditionalFields) 
    | extend ClassName = PNP.ClassName 
    | extend PnPDeviceId = PNP.DeviceId 
    | extend DeviceDescription = PNP.DeviceDescription
    | extend VendorId = split(PNP.VendorIds,@'\r\n\t\t')
    | extend VendorId = PNP.VendorIds
    | where PnPDeviceId startswith @'USB\'
    | where PnPDeviceId has_any('VID_05AC','VID_0502','VID_2080','VID_0fca','VID_18d1','VID_15bf','VID_0bb4','VID_04e8','VID_0bb4',
        'VID_12d1','VID_0482','VID_1949','VID_2006','VID_1004','VID_22b8','VID_0421','VID_22d9','VID_04e8','VID_0492','VID_0681',
        'VID_054c','VID_0fce','VID_1bbb','VID_2717','VID_19d2','VID_6353&PID_2019','VID_1256&PID_2670')
    //| where DeviceDescription contains @'Apple'
    | project Timestamp, DeviceName, DeviceId, ClassName, DeviceDescription, PnPDeviceId, VendorId, ReportId
    | join kind = inner(loggedOnUser) on DeviceId
    | summarize arg_max(Timestamp1, *) by Timestamp
    | project-away DeviceId1, Timestamp1  
    | sort by Timestamp desc
