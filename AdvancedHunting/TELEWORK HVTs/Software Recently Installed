//identify subset of machines and query for recently installed software (24hours)
//jesse.esquivel@microsoft.com
//filter to machines with deviceRegistry tag or to a machine group
let targetDevices = DeviceInfo
        | where Timestamp > ago(24h)
        | where RegistryDeviceTag == "DEVICEREGISTRYTAG"
        | project Timestamp, DeviceId, DeviceName, RegistryDeviceTag, ReportId;
DeviceRegistryEvents
| where Timestamp > ago(24h)
| where ActionType == "RegistryKeyCreated"
| where RegistryKey startswith @"HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"
| where InitiatingProcessCommandLine !startswith @"MsiExec.exe -Embedding" //filter out noise, there could be more
| where InitiatingProcessCommandLine != "msiexec.exe /V" //filter out noise, there could be more
| extend SoftwareName=(split(RegistryKey, @"\")[6])
| project Timestamp, DeviceId, DeviceName, ActionType, SoftwareName, RegistryKey, InitiatingProcessAccountDomain, InitiatingProcessAccountName, InitiatingProcessCommandLine, ReportId
| join kind = inner(targetDevices) on DeviceId
| summarize arg_max(ReportId, *) by Timestamp
| sort by Timestamp desc
